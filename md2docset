#!/usr/bin/env perl

## Each .md file might have a comment at the top:
##
##  <!-- dash: <Name> | <Type> | <Attrs> -->
##
## Name is the name of the page (duh).
## Type is the Dash Entry Type (https://kapeli.com/docsets)
## Attrs indicates if anything on the page should be added to the ToC.
##
## Attrs are specified like
##
##  ##:Section
##
## where the "##" indicates the markup and Section is a Dash Entry
## Type.  Multiple attrs can be specified, they should be separated by
## a semicolon, e.g.,
##
##  <!-- dash: Some Page | Guide | ##:Section; ###:Entry -->




use strict;
use autodie;
use 5.10.0;

use File::Path qw[ make_path ];
use FindBin;
use URI::Escape;
use Data::Dumper;

our $INPUT       = "$FindBin::Bin/../nitrogen_core/doc/markdown";
our $OUTPUT_BASE = "$FindBin::Bin/Nitrogen.docset";
our $DB          = "$OUTPUT_BASE/Contents/Resources/docSet.dsidx";
our $HTML        = "$OUTPUT_BASE/Contents/Resources/Documents";
our $PLIST       = "$OUTPUT_BASE/Contents/Info.plist";
our $CSS         = "$HTML/nitrogen.css";

our $REFERENCE_PLIST = "$FindBin::Bin/Info.plist";

my $gh = 'https://raw.githubusercontent.com/nitrogen';
our @SOURCE_CSS_URLs = (
                        "$gh/nitrogen_core/master/www/nitrogen.css",
                        "$gh/NitrogenProject.com/master/static/css/style.css"
                       );

# Create the docset directory structure.
make_path($HTML);

# Move the plist into place.
`cp $FindBin::Bin/Info.plist $PLIST`;

# Create the DB.
create_db();

# Generate a list of the MD files to be processed.
opendir(my $dh, $INPUT);
#my @files = grep { /.*\.md/ } readdir($dh);
my @files = grep { /^a.+\.md/ } readdir($dh);
closedir($dh);

# Generate the reference CSS file.
generate_css($CSS, @SOURCE_CSS_URLs);

# Generate the HTTML with pandoc.
my @metadata = generate_html(@files);

# Populate the Dash index.
build_index(@metadata);

sub generate_css {
  my ($outfile, @source_urls) = @_;
  my @css_data = map { `wget -O - $_ 2>/dev/null` } @source_urls;
  open my $fh, '>', $outfile;
  print $fh join "" => @css_data;
  close $fh;
}

sub generate_html {
  my @metadata = ();
  for my $file (@_) {
    my ($page_name, $page_type, $page_attrs) = parse_file($file);
    my $out = ($file =~ s/md$/html/r);
    my $md = qq!--metadata title="$page_name"!;
    my $css = "--css nitrogen.css";
    my $mounts = "-v $INPUT:/pandoc -v $HTML:/output";
    my $img = 'dalibo/pandocker';
    <<`EOF`;
docker run --rm -u $> $mounts $img -o /output/$out --ascii $css -s $md $file
EOF
    push @metadata => [$out, $page_name, $page_type, $page_attrs];
  }
  return @metadata;
}

sub create_db {
  unlink $DB if -e $DB;
  my ($table, $index) = (table_definition(), index_definition());
  `sqlite3 $DB $table`;
  `sqlite3 $DB $index`;
}  

sub build_index {
  for my $md (@_) {
    my ($file, $name, $type, $attrs) = @{$md};
    my $stmt = make_stmt($file, $name, $type);
    `sqlite3 $DB '$stmt'`;
  }
}

sub make_stmt {
  my ($file, $name, $type) = @_;
  <<EOF;
INSERT OR IGNORE INTO searchIndex(name, type, path) 
VALUES ("$name", "$type", "$file")
EOF
}

sub table_definition {
  <<EOF;
   'CREATE TABLE searchIndex(
      id INTEGER PRIMARY KEY, 
      name TEXT, 
      type TEXT, 
      path TEXT)';
EOF
}

sub index_definition {
  <<EOF;
  'CREATE UNIQUE INDEX anchor ON searchIndex (name, type, path)';
EOF
}

sub parse_file {
  my $filename = shift;
  my $html_file = ($filename =~ s/.md$/.html/r);
  my ($name, $type, $attrs, @rewritten_lines) = ();
  my $source_file = $INPUT . "/" . $filename;
  open my $fh, '<', $source_file;
  say "Reading $source_file";
  while (<$fh>) {
    if ($_ =~ /<!--\s+dash:\s+(.+?)\s+-->/) {
      ($name, $type, $attrs) = split /\s*\|\s*/, $1;
    }

    # maybe add a table of contents anchor.
    my %TOC = make_toc_lookup($attrs);
    for my $markup (keys %TOC) {
      s/^\s*($markup)\s+(.+)/toc_markup($1, $2, $TOC{$markup})/e;
    }

    # maybe add a function.
    s/(`(wf:.+?)\((.*?)\).*?`)/register_fn($1, $2, $3, $html_file)/eg;

    # sub .html for .md in links
    push @rewritten_lines => s/\((.+?)\.md\)/\($1.html\)/gr;
  }
  close $fh;
  
  open my $fh, '>', $source_file;
  print $fh @rewritten_lines;
  return ($name, $type, $attrs);
}

sub make_toc_lookup {
  my $attrs = shift;
  my %toc = ();
  for my $attr (split /\s*;\s*/ => $attrs) {
    my ($markup, $type) = split /\s*:\s*/ => $attr;
    $toc{$markup} = $type;
  }
  return %toc;
}
  

sub toc_markup {
  my ($markup, $value, $type) = @_;
  my $enc_value = uri_escape($value);
  my $anchor = "//apple_ref/cpp/$type/$enc_value";
  my $tag = qq!<a name="$anchor" class="dashAnchor">!;
  return qq!$markup $tag$value</a>!;
}

sub register_fn {
  my ($phrase, $fn, $args, $filename) = @_;
  my $arity = scalar(split /\s*,\s*/, $args);
  my $anchor = "//apple_ref/cpp/Function/$fn%2F$arity";
  my $stmt = make_stmt("$filename#$anchor", "$fn/$arity", "Function");
  `sqlite3 $DB '$stmt'`;
  return qq!<a name="$anchor" class="dashAnchor">$phrase</a>!;
}
